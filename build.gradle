plugins {
    id "fabric-loom" version "1.8.9"
    id "babric-loom-extension" version "1.8.6"
    id "maven-publish"
}

version = project.mod_version
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

loom {
    accessWidenerPath = file("src/main/resources/${project.mod_id}.accesswidener")

    // Our custom manifest replaces LWJGL2 with our LWJGL3 injector.
    customMinecraftMetadata = ""
}

babric {
    // Disable gambac library (M1 colours fix.)
    // Our LWJGL3 injector already deals with it.
    disableM1Fixes.set(true)
}

repositories {
    // Used for Nostalgia mappings.
    maven {
        name = "Wisp Forest"
        url = "https://maven.wispforest.io"
    }

    // Used for projects without a maven, but on GitHub repos with working build scripts.
    maven {
        name = "Jitpack"
        url = "https://jitpack.io/"
    }
}

dependencies {
    // Rhino is the JS engine: https://github.com/mozilla/rhino/releases
    // Archived versions: https://www-archive.mozilla.org/rhino/rhino16r2
    implementation "org.mozilla:rhino:1.7.15"

    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "me.alphamode:nostalgia:${project.nostalgia_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    // Override ASM to support Java 23
    implementation "org.ow2.asm:asm:${project.asm_version}"
    implementation "org.ow2.asm:asm-analysis:${project.asm_version}"
    implementation "org.ow2.asm:asm-commons:${project.asm_version}"
    implementation "org.ow2.asm:asm-tree:${project.asm_version}"
    implementation "org.ow2.asm:asm-util:${project.asm_version}"

    // Fabric-ASM allows us to extend enums.
    modImplementation "com.github.Chocohead:Fabric-ASM:v2.3"

    // FastUtil provides specialized collection types.
    implementation "it.unimi.dsi:fastutil-core:8.5.12"

    // Brigadier provides modern command parsing.
    modImplementation "com.mojang:brigadier:1.2.9"
}

processResources {
    filesMatching("fabric.mod.json") {
        expand project.properties
    }
}

java {
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and
    // to the "build" task if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()

    sourceCompatibility = targetCompatibility = JavaVersion.VERSION_17
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.base.archivesName.get()}"}
    }
}

// For information on how to set up publishing,
// see https://docs.gradle.org/current/userguide/publishing_maven.html
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    repositories {
        // Add repositories to publish to here.
        // Notice: This block does NOT have the same function as the block in the top level.
        // The repositories here will be used for publishing your artifact,
        // not for retrieving dependencies.
    }
}
