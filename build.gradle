plugins {
    // Babric loom version can be checked here: https://github.com/babric/fabric-loom/tags
    id 'babric-loom' version '1.1-SNAPSHOT'
    id 'maven-publish'
}

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group
def modId = project.mod_id

loom {
    gluedMinecraftJar()
    customMinecraftManifest.set("https://babric.github.io/manifest-polyfill/${minecraft_version}.json")
    accessWidenerPath = file("src/main/resources/${modId}.accesswidener")
}

repositories {
    maven {
        name = 'Babric'
        url = 'https://maven.glass-launcher.net/babric'
    }
    maven {
        name 'LegacyFabric'
        url = "https://repo.legacyfabric.net/repository/legacyfabric"
    }
    maven {
        name 'Jitpack'
        url 'https://jitpack.io/'
    }
    maven {
        name 'HalfOf2'
        url 'https://storage.googleapis.com/devan-maven/'
    }
    maven {
        name 'LWJGL3'
        url 'https://oss.sonatype.org/content/repositories/releases/'
    }
    mavenLocal()
}

import org.gradle.internal.os.OperatingSystem

project.ext.lwjglVersion = "3.3.2"
switch (OperatingSystem.current()) {
    case OperatingSystem.LINUX:
        project.ext.lwjglNatives = "natives-linux"
        break
    case OperatingSystem.MAC_OS:
        project.ext.lwjglNatives = "natives-macos"
        break
    case OperatingSystem.WINDOWS:
        def osArch = System.getProperty("os.arch")
        project.ext.lwjglNatives = osArch.contains("64")
                ? "natives-windows${osArch.startsWith("aarch64") ? "-arm64" : ""}"
                : "natives-windows-x86"
        break
}

dependencies {
    // Adds LWJGL3 wrapper
    implementation("com.github.Adventurecraft-Awakening:AC-LWJGL3-Injector:0.0.1")

    implementation platform("org.lwjgl:lwjgl-bom:$lwjglVersion")

    runtimeOnly "org.lwjgl:lwjgl::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-glfw::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-openal::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-opengl::$lwjglNatives"

    // Compiles rhino, our javascript dependency, releases can be found here: https://github.com/mozilla/rhino/releases
    // There are more archived versions on this website: https://www-archive.mozilla.org/rhino/rhino16r2
    implementation 'rhino:js:1.7R2'

    include(implementation(annotationProcessor("com.github.llamalad7.mixinextras:mixinextras-fabric:0.2.0-beta.6")))

    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.legacyfabric:beta-yarn:${project.yarn_version}"
    modImplementation "babric:fabric-loader:${project.loader_version}"

    implementation "org.slf4j:slf4j-api:1.8.0-beta4"
    implementation "org.apache.logging.log4j:log4j-slf4j18-impl:2.18.0"

    implementation('com.github.Chocohead:Fabric-ASM:v2.3')

    implementation 'com.google.guava:guava:31+'
    implementation 'org.apache.commons:commons-lang3:3.12.0'

    implementation('it.unimi.dsi:fastutil-core:8.5.12')
}

configurations.configureEach {
    // Removes LWJGL2 dependencies
    exclude group: "org.lwjgl.lwjgl"
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

java {
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
        // Notice: This block does NOT have the same function as the block in the top level.
        // The repositories here will be used for publishing your artifact, not for
        // retrieving dependencies.
    }
}
